@echo off
setlocal
REM Build With/Out Installing (BWOI) Example
REM .
REM Extracts the MSIs in the Microsoft GDK to set up BWOI
REM .
if %1.==. goto usage

if NOT EXIST "%1\Microsoft GDK x86 Common-x86_en-us.msi" goto nogdkfound

if %ExtractedFolder%.==. goto :missingextracted

if EXIST "%ExtractedFolder%\Microsoft GDK" goto :overwritewarn
if NOT EXIST "%ExtractedFolder%" ( md "%ExtractedFolder%"
if errorlevel 1 goto :makefailed )

REM The Windows Kits folder generated by the GDK extraction is not needed and can cause conflicts later.
if NOT EXIST "%ExtractedFolder%\Windows Kits" set CleanupKits=1

for %%1 in ("%1\*Kits*.msi") do (echo "Extracting: %%1"
msiexec.exe /quiet /a "%%1" TARGETDIR="%ExtractedFolder%"
if errorlevel 1 goto :msifailed)

for %%1 in ("%1\*Gaming*.msi") do (echo "Extracting: %%1"
msiexec.exe /quiet /a "%%1" TARGETDIR="%ExtractedFolder%"
if errorlevel 1 goto :msifailed)

for %%1 in ("%1\*GDK*.msi") do (echo "Extracting: %%1"
msiexec.exe /quiet /a "%%1" TARGETDIR="%ExtractedFolder%"
if errorlevel 1 goto :msifailed)

for %%1 in ("%1\*GRDK*.msi") do (echo "Extracting: %%1"
msiexec.exe /quiet /a "%%1" TARGETDIR="%ExtractedFolder%"
if errorlevel 1 goto :msifailed)

for %%1 in ("%1\*GXDK*.msi") do (echo "Extracting: %%1"
msiexec.exe /quiet /a "%%1" TARGETDIR="%ExtractedFolder%"
if errorlevel 1 goto :msifailed)

if %CleanupKits%.==1. rmdir /s /q "%ExtractedFolder%\Windows Kits"
goto :eof

:usage
echo extractgdk gdk-source-installers-folder
exit /b 0

:nogdkfound
echo Can't find MSI files for the Microsoft GDK
exit /b 1

:missingextracted
echo extractgdk requires the ExtractedFolder environment variable set by setenv.cmd
exit /b 1

:overwritewarn
echo ExtractedFolder for Microsoft GDK already exists. Delete and rerun.
echo Conflicting folder: %ExtractedFolder%\Microsoft GDK
exit /b 1

:makefailed
echo Failed to create ExtractedFolder
exit /b /1

:msifailed
echo ERROR: MSI Extract failed
if %CleanupKits%.==1. rmdir /s /q "%ExtractedFolder%\Windows Kits"
exit /b 1
